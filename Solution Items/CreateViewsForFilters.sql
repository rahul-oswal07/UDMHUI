/*
 * AUTHORS: AISHWARYA RAO,SHRAVANI HUGGI
 * CREATION DATE: 19/08/2015
 * DESCRIPTION: THIS SQL CODE APPLIES BUSINESS TRANSFORMATION FOR THE TECHNICAL FILTERS
 * REVISION HISTORY:
 *------------------------------------------------------------------------------------------------------------------
 * VERSION      DATE            DEVELOPER                               DESCRIPTION
 *------------------------------------------------------------------------------------------------------------------
    1.0         19/08/2015      AISHWARYA RAO, SHRAVANI HUGGI           BASED ON MAPPING SPEC VERSION V0.9
    2.0         04/11/2015      SHRAVANI HUGGI                          ADDED INSURANCEPOLICIES FOUR FILTERS
                                                                        BASED ON MAPPING SPEC VERSION V1.2
    3.0         26/11/2015      SHRAVANI                                MODIFIED BASED ON MAPPING SPEC V1.3
    4.0         04/12/2015      AISHWARYA RAO                           ADDED FILTER PAYMENTELEMENTS<CurrentAtMigr>;
                                                                        MODIFIED FILTERS ON CWPOSTALADDRESS
    5.0         05/12/2015      AISHWARYA RAO                           ADDED FILTER FOR HLDLNKS
    6.0         10/12/2015      AISHWARYA RAO                           ADDED FILTER FOR APPSUB
 *------------------------------------------------------------------------------------------------------------------
 */

--CWPOSTALADDRESS<LIVING>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_CWPOSTAL_ADDRESS_LIVING]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_CWPOSTAL_ADDRESS_LIVING]
GO

CREATE VIEW [DBO].[VW_FILTER_CWPOSTAL_ADDRESS_LIVING]
AS
WITH
FILTER_CWPOSTAL_LIVING_ALL AS (
  SELECT CWPOSTADDS.ADDRESS1
        ,CWPOSTADDS.ADDRESS2
        ,CWPOSTADDS.ADDRESS3
        ,CWPOSTADDS.ADDRESS4
        ,CWPOSTADDS.ADDRESS5
        ,CWPOSTADDS.ADDRESS6
        ,CWPOSTADDS.ADDRESS7
        ,CWPOSTADDS.COUNTRY
        ,CWPOSTADDS.CUSTOMERNO
        ,CWPOSTADDS.GONEAWAY
        ,CWPOSTADDS.POSTCODE
        ,CWPOSTADDS.USAGE
        ,ROW_NUMBER() OVER (
           PARTITION BY
             CUSTOMERNO
           ORDER BY
             LASTUPDATEDATE DESC
           ) AS CWPOSTAL_LIVE_RN
    FROM TB_SRC_CWPOSTALADDRESS CWPOSTADDS
   CROSS JOIN TB_UDMH_CTL
   WHERE CAST(SUBSTRING(EFFECTIVEFROM,1,8) AS DATE) <= WS_MIGR_DATE
     AND( CAST(SUBSTRING(EFFECTIVETO,1,8) AS DATE) IS NULL
           OR CAST(SUBSTRING(EFFECTIVETO,1,8) AS DATE) >= WS_MIGR_DATE )
     AND USAGE = 1088002  --Means Living
),
CWPOSTAL_LIVING AS (
  SELECT *
    FROM FILTER_CWPOSTAL_LIVING_ALL
   WHERE CWPOSTAL_LIVE_RN = 1
)
SELECT *
  FROM CWPOSTAL_LIVING
GO

--CWPOSTALADDRESS<CORRESPONDENCE>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_CWPOSTAL_ADDRESS_CORRESPONDENCE]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_CWPOSTAL_ADDRESS_CORRESPONDENCE]
GO

CREATE VIEW [DBO].[VW_FILTER_CWPOSTAL_ADDRESS_CORRESPONDENCE]
AS
WITH
FILTER_CWPOSTAL_CORR_ALL AS (
  SELECT CWPOSTADDS.ADDRESS1
        ,CWPOSTADDS.ADDRESS2
        ,CWPOSTADDS.ADDRESS3
        ,CWPOSTADDS.ADDRESS4
        ,CWPOSTADDS.ADDRESS5
        ,CWPOSTADDS.ADDRESS6
        ,CWPOSTADDS.ADDRESS7
        ,CWPOSTADDS.COUNTRY
        ,CWPOSTADDS.CUSTOMERNO
        ,CWPOSTADDS.GONEAWAY
        ,CWPOSTADDS.POSTCODE
        ,CWPOSTADDS.USAGE
        ,ROW_NUMBER() OVER (
           PARTITION BY
             CUSTOMERNO
           ORDER BY
             LASTUPDATEDATE DESC
           ) AS CWPOSTAL_CORR_RN
    FROM TB_SRC_CWPOSTALADDRESS AS CWPOSTADDS
   CROSS JOIN TB_UDMH_CTL
   WHERE CAST(SUBSTRING(EFFECTIVEFROM,1,8) AS DATE) <= WS_MIGR_DATE
     AND( CAST(SUBSTRING(EFFECTIVETO,1,8) AS DATE) IS NULL
          OR CAST(SUBSTRING(EFFECTIVETO,1,8) AS DATE) >= WS_MIGR_DATE )
     AND USAGE = 1           ------- NEED TO CHECK (CODE XXXX TBD)-------
),
CWPOSTAL_CORRESPONDENCE AS (
  SELECT *
    FROM FILTER_CWPOSTAL_CORR_ALL
   WHERE CWPOSTAL_CORR_RN = 1
)
SELECT *
  FROM CWPOSTAL_CORRESPONDENCE
GO

--CWPOSTALADDRESS<ACCOUNT>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_CWPOSTAL_ADDRESS_ACCOUNT]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_CWPOSTAL_ADDRESS_ACCOUNT]
GO

CREATE VIEW [DBO].[VW_FILTER_CWPOSTAL_ADDRESS_ACCOUNT]
AS
WITH
FILTER_CWPOSTAL_ACNT_ALL AS (
  SELECT CWPOSTADDS.ADDRESS1
        ,CWPOSTADDS.ADDRESS2
        ,CWPOSTADDS.ADDRESS3
        ,CWPOSTADDS.ADDRESS4
        ,CWPOSTADDS.ADDRESS5
        ,CWPOSTADDS.ADDRESS6
        ,CWPOSTADDS.ADDRESS7
        ,CWPOSTADDS.COUNTRY
        ,CWPOSTADDS.CUSTOMERNO
        ,CWPOSTADDS.GONEAWAY
        ,CWPOSTADDS.POSTCODE
        ,CWPOSTADDS.USAGE
        ,ROW_NUMBER() OVER (
           PARTITION BY
             CUSTOMERNO
           ORDER BY
             LASTUPDATEDATE DESC
           ) AS CWPOSTAL_ACNT_RN
    FROM TB_SRC_CWPOSTALADDRESS AS CWPOSTADDS
   CROSS JOIN TB_UDMH_CTL
   WHERE CAST(SUBSTRING(EFFECTIVEFROM,1,8) AS DATE) <= WS_MIGR_DATE
     AND( CAST(SUBSTRING(EFFECTIVETO,1,8) AS DATE) IS NULL
          OR CAST(SUBSTRING(EFFECTIVETO,1,8) AS DATE) >= WS_MIGR_DATE )
     AND USAGE = 1           ------- NEED TO CHECK HAS (CODE XXXX TBD)-------
),
CWPOSTAL_ACCOUNT AS (
  SELECT * 
    FROM FILTER_CWPOSTAL_ACNT_ALL
   WHERE CWPOSTAL_ACNT_RN = 1
)
SELECT *
  FROM CWPOSTAL_ACCOUNT
GO

--ACPEDYMD<YEARENDBEFOREMIGR>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_ACPEDYMD_YEARENDBEFOREMIGR]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_ACPEDYMD_YEARENDBEFOREMIGR]
GO

CREATE VIEW [DBO].[VW_FILTER_ACPEDYMD_YEARENDBEFOREMIGR]
AS
WITH
FILTER_ACPEDYMD_YEARENDBEFOREMIGR AS (
  SELECT ACPEDACNTNO
        ,ACPEDAREARSOS
        ,ACPEDBALOS
        ,ACPEDCALINTGROSDB
        ,ACPEDCAPITALBAL
        ,ACPEDFORINTGROSDB
        ,ACPEDGROSRT
        ,ACPEDINTACGRDB
        ,ACPEDINTPAIDGRDB
        ,ACPEDMTHSINAREARS
        ,ACPEDPERIOD
        ,ACPEDSUBACNO
        ,ACPEDTRUECALINTGROSDB
    FROM TB_SRC_ACPEDYMD
   CROSS JOIN TB_UDMH_CTL AS CTRL
   WHERE ACPEDPERIOD = DBO.FN_LASTDAY_OF_CURYEAR(DATEADD(YEAR, -1, CTRL.WS_MIGR_DATE ))
 )
 SELECT *
   FROM FILTER_ACPEDYMD_YEARENDBEFOREMIGR
GO

--ACPEDYMD<MONTHENDBEFOREMIGR>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_ACPEDYMD_MONTHENDBEFOREMIGR]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_ACPEDYMD_MONTHENDBEFOREMIGR]
GO

CREATE VIEW [DBO].[VW_FILTER_ACPEDYMD_MONTHENDBEFOREMIGR]
AS
WITH
FILTER_ACPEDYMD_MONTHENDBEFOREMIGR AS (
  SELECT ACPEDACNTNO
        ,ACPEDAREARSOS
        ,ACPEDBALOS
        ,ACPEDCALINTGROSDB
        ,ACPEDCAPITALBAL
        ,ACPEDFORINTGROSDB
        ,ACPEDGROSRT
        ,ACPEDINTACGRDB
        ,ACPEDINTPAIDGRDB
        ,ACPEDMTHSINAREARS
        ,ACPEDPERIOD
        ,ACPEDSUBACNO
        ,ACPEDTRUECALINTGROSDB
    FROM TB_SRC_ACPEDYMD
  CROSS JOIN TB_UDMH_CTL AS CTRL
 WHERE ACPEDPERIOD = dbo.FN_END_OF_MONTH(DATEADD(MONTH, -1, CTRL.WS_MIGR_DATE ))
 )
 SELECT *
   FROM FILTER_ACPEDYMD_MONTHENDBEFOREMIGR
GO

--AC01<MAIN>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_AC01_MAIN]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_AC01_MAIN]
GO

CREATE VIEW [DBO].[VW_FILTER_AC01_MAIN]
AS
WITH 
FILTER_AC01 AS (
  SELECT ACACNTNO 
    FROM TB_SRC_AC01
  GROUP BY
        ACACNTNO
       ,ACGACTTYPE
  HAVING ACGACTTYPE = 'M'
     AND COUNT(1) >= 1
),
FILTER_AC01_MAIN AS (
  SELECT FILTER_AC01.*
        ,AC01.ACACINTGROSDB
        ,AC01.ACACTCODE
        ,AC01.ACAREARSPYMNTOS
        ,AC01.ACACTUALADVANCE
        ,AC01.ACAGREDADVANCE
        ,AC01.ACAREARSOS
        ,AC01.ACBALBFWD
        ,AC01.ACBALOS
        ,AC01.ACCAPITALBAL
        ,AC01.ACCLAS
        ,AC01.ACCLRDATE
        ,AC01.ACCURENTTRM
        ,AC01.ACFISCINTGROSDB
        ,AC01.ACFORINTGROSDB
        ,AC01.ACGACTTYPE
        ,AC01.ACHBRNCODE
        ,AC01.ACHFIRSTBUYER
        ,AC01.ACHINCMULT
        ,AC01.ACHLASTCHNGBY
        ,AC01.ACHLATESTVALUATION
        ,AC01.ACHLDERTYPE
        ,AC01.ACHNOTICEINDICATOR
        ,AC01.ACHNOTICEPERIOD
        ,AC01.ACHPORTEDACNTNO
        ,AC01.ACHPOSESIONDATE
        ,AC01.ACHPURCHPROPSEQNO
        ,AC01.ACHTITLE
        ,AC01.ACINCEPDATE
        ,AC01.ACINTCCODE
        ,AC01.ACINTRGROSRT
        ,AC01.ACLASTCAPDATEDB
        ,AC01.ACMAXMTHSINAREARS
        ,AC01.ACORIGINALTRM
        ,AC01.ACPORTEDSUBACNO
        ,AC01.ACPROCESSTATUS
        ,AC01.ACPURPOSECODE
        ,AC01.ACRETENTIONBAL
        ,AC01.ACSCHEDTRMDATE
        ,AC01.ACSMNTBAL
        ,AC01.ACSMNTDUEDATE
        ,AC01.ACSMNTLASTDATE
        ,AC01.ACSOCREGCODE
        ,AC01.ACSUBACNO
        ,AC01.ACTOTADVANCEOS
        ,AC01.ACTRMBANDSTARTDATE
        ,AC01.ACTRMINTERVAL
        ,AC01.ADDRESS1
        ,AC01.ADDRESS2
        ,AC01.ADDRESS3
        ,AC01.ADDRESS4
        ,AC01.ADDRESS5
        ,AC01.ADDRESSNO
        ,AC01.POSTCODE
        ,AC01.RCCODE
    FROM FILTER_AC01
    JOIN TB_SRC_AC01 AS AC01
      ON AC01.ACACNTNO = FILTER_AC01.ACACNTNO
   WHERE AC01.ACSUBACNO = 1
     AND AC01.ACPROCESSTATUS <> 1
)
SELECT *
  FROM FILTER_AC01_MAIN
GO

--ACNTLNKS<MORTGAGE>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_ACNTLNKS_MORTGAGE]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_ACNTLNKS_MORTGAGE]
GO

CREATE VIEW [DBO].[VW_FILTER_ACNTLNKS_MORTGAGE]
AS
WITH 
FILTER_SRC AS (
  SELECT
        ACLACNTNO
       ,ACLCLSEQNO
       ,ACLSUBACNO
       ,FIRST_VALUE (ACLCLUSAGE) OVER (
        PARTITION BY
          ACLACNTNO
         ,ACLCLSEQNO
        ORDER BY
          ACLLASTCHNGDATE DESC
        ) AS ACLCLUSAGE
       ,FIRST_VALUE (ACLEXTRACTREQUIRED) OVER (
        PARTITION BY
          ACLACNTNO
         ,ACLCLSEQNO
        ORDER BY
          ACLLASTCHNGDATE DESC
        ) AS ACLEXTRACTREQUIRED
       ,CASE
          WHEN MIN(ACLHLDERPOS) OVER (
               PARTITION BY
                 ACLACNTNO
                ,ACLCLSEQNO ) = 0 THEN 0
          ELSE
          FIRST_VALUE (ACLHLDERPOS) OVER (
          PARTITION BY
            ACLACNTNO
           ,ACLCLSEQNO
          ORDER BY
            ACLLASTCHNGDATE DESC )
        END AS ACLHLDERPOS
       ,FIRST_VALUE (ACLLASTCHNGBY) OVER (
        PARTITION BY
          ACLACNTNO
         ,ACLCLSEQNO
        ORDER BY
          ACLLASTCHNGDATE DESC 
        ) AS ACLLASTCHNGBY
       ,MAX (ACLLASTCHNGDATE) OVER (
        PARTITION BY 
          ACLACNTNO
         ,ACLCLSEQNO 
        ) AS ACLLASTCHNGDATE
       ,FIRST_VALUE (ACLOLDNO) OVER (
        PARTITION BY
          ACLACNTNO
         ,ACLCLSEQNO
        ORDER BY
          ACLLASTCHNGDATE DESC 
        ) AS ACLOLDNO
       ,FIRST_VALUE (ACLSOCCTRL) OVER (
        PARTITION BY
          ACLACNTNO
         ,ACLCLSEQNO
        ORDER BY
          ACLLASTCHNGDATE DESC 
        ) AS ACLSOCCTRL
       ,FIRST_VALUE (ACLSOCSEQNO) OVER (
        PARTITION BY
          ACLACNTNO
         ,ACLCLSEQNO
        ORDER BY
          ACLLASTCHNGDATE DESC 
        ) AS ACLSOCSEQNO
       ,ROW_NUMBER() OVER (
          PARTITION BY
            ACLACNTNO
           ,ACLCLSEQNO
          ORDER BY 
            ACLLASTCHNGDATE DESC 
        ) AS RN
    FROM TB_SRC_ACNTLNKS 
    JOIN TB_SRC_AC01_MAIN AS AC01_MAIN
      ON ACLACNTNO = AC01_MAIN.ACACNTNO
),
FILTER_ACNTLNKS_MORTGAGE AS (
  SELECT *
    FROM FILTER_SRC
   WHERE RN = 1
)
SELECT *
  FROM FILTER_ACNTLNKS_MORTGAGE
GO

--AC01<LOWESTFORANAPP>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_AC01_LOWESTFORANAPP]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_AC01_LOWESTFORANAPP]
GO

CREATE VIEW [DBO].[VW_FILTER_AC01_LOWESTFORANAPP]
AS
WITH
FILTER_ALL_AC01FORAPP AS (
  SELECT AC01.ACACNTNO
        ,AC01.ACINCEPDATE
        ,AC01.ACSUBACNO
        ,AC01.ACHBRNCODE
        ,AC01.ACPURPOSECODE
        ,AC01.ACSOCREGCODE
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             AC01.ACACNTNO
            ,AC01.ACINCEPDATE
            ORDER BY
              AC01.ACSUBACNO
          ) AS RN
    FROM TB_SRC_AC01_MAIN AS AC01_MAIN
    JOIN TB_SRC_AC01 AS AC01
      ON AC01_MAIN.ACACNTNO = AC01.ACACNTNO
 ),
FILTER_LOWEST_AC01FORAPP AS (
  SELECT * 
    FROM FILTER_ALL_AC01FORAPP
   WHERE RN = 1
)
SELECT *
  FROM FILTER_LOWEST_AC01FORAPP
GO

--AC01<FIRSTLIVEORLASTREDEEMSUB>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_AC01_FIRSTLIVEORLASTREDEEMSUB]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_AC01_FIRSTLIVEORLASTREDEEMSUB]
GO

CREATE VIEW [DBO].[VW_FILTER_AC01_FIRSTLIVEORLASTREDEEMSUB]
AS
WITH 
FILTER_AC01_1STL_LSTR_ALL AS (
  SELECT ACACNTNO
        ,ACACINTGROSDB
        ,ACACTCODE
        ,ACACTUALADVANCE
        ,ACAGREDADVANCE
        ,ACAREARSOS
        ,ACAREARSPYMNTOS
        ,ACBALBFWD
        ,ACBALOS
        ,ACCAPITALBAL
        ,ACCLAS
        ,ACCLRDATE
        ,ACCURENTTRM
        ,ACFISCINTGROSDB
        ,ACFORINTGROSDB
        ,ACGACTTYPE
        ,ACHBRNCODE
        ,ACHFIRSTBUYER
        ,ACHINCMULT
        ,ACHLASTCHNGBY
        ,ACHLATESTVALUATION
        ,ACHLDERTYPE
        ,ACHNOTICEINDICATOR
        ,ACHNOTICEPERIOD
        ,ACHPORTEDACNTNO
        ,ACHPOSESIONDATE
        ,ACHPURCHPROPSEQNO
        ,ACHTITLE
        ,ACINCEPDATE
        ,ACINTCCODE
        ,ACINTRGROSRT
        ,ACLASTCAPDATEDB
        ,ACMAXMTHSINAREARS
        ,ACORIGINALTRM
        ,ACPORTEDSUBACNO
        ,ACPROCESSTATUS
        ,ACPURPOSECODE
        ,ACRETENTIONBAL
        ,ACSCHEDTRMDATE
        ,ACSMNTBAL
        ,ACSMNTDUEDATE
        ,ACSMNTLASTDATE
        ,ACSOCREGCODE
        ,ACSUBACNO
        ,ACTOTADVANCEOS
        ,ACTRMBANDSTARTDATE
        ,ACTRMINTERVAL
        ,ADDRESS1
        ,ADDRESS2
        ,ADDRESS3
        ,ADDRESS4
        ,ADDRESS5
        ,ADDRESSNO
        ,POSTCODE
        ,RCCODE
        ,CASE 
           WHEN MAX(
                  CASE ACPROCESSTATUS 
                    WHEN 2 THEN 'Y'
                  END
                  ) OVER (
                  PARTITION BY 
                    ACACNTNO ) = 'Y' THEN
                MIN(
                  CASE ACPROCESSTATUS
                    WHEN 2 THEN ACSUBACNO
                  END
                  ) OVER ( 
                  PARTITION BY
                    ACACNTNO
                  )
         END AS AC01_1ST_LIVE_SUB
        ,CASE
           WHEN MAX(
                  CASE ACPROCESSTATUS
                    WHEN 2 THEN 'Y'
                  END
                  ) OVER (
                  PARTITION BY 
                    ACACNTNO ) IS NULL THEN
           ROW_NUMBER() OVER (
             PARTITION BY
               ACACNTNO
             ORDER BY
               ACCLRDATE DESC
              ,ACSUBACNO
             )
         END AS AC01_LAST_REDEEM_RN
    FROM TB_SRC_AC01 
 ),
FILTER_AC01_1STL_LSTR AS (
  SELECT *
    FROM FILTER_AC01_1STL_LSTR_ALL
   WHERE AC01_1ST_LIVE_SUB = ACSUBACNO
     OR AC01_LAST_REDEEM_RN = 1
)
SELECT *
  FROM FILTER_AC01_1STL_LSTR
GO



--AA32P<TIERCHANGE>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_AA32P_TIERCHANGE]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_AA32P_TIERCHANGE]
GO

CREATE VIEW [DBO].[VW_FILTER_AA32P_TIERCHANGE]
AS
WITH AA32P_ALL AS (
  SELECT ARNGNO
        ,INSTLAMOUNT
        ,INSTLDATE
        ,ROW_NUMBER() OVER (
          PARTITION BY ARNGNO, INSTLDATE
          ORDER BY REVNNO DESC) AS RN
    from TB_SRC_AA32P
),
AA32P_LATEST AS (
  SELECT *
    FROM AA32P_ALL
WHERE RN = 1
),
FILTER_AA32P_PREVNEXT AS (
  SELECT ARNGNO
        ,INSTLAMOUNT
        ,INSTLDATE
        ,LAG (INSTLAMOUNT,1) OVER (
           PARTITION BY
             ARNGNO
           ORDER BY
             INSTLDATE
         ) AS PREV_INSTLAMOUNT
        ,LEAD (INSTLAMOUNT,1) OVER (
           PARTITION BY
             ARNGNO
           ORDER BY 
             INSTLDATE
         ) AS NEXT_INSTLAMOUNT
     FROM AA32P_LATEST
),
FILTER_AA32P_TAG_TIERSEQ AS (
  SELECT *
         ,SUM(
            CASE 
              WHEN PREV_INSTLAMOUNT IS NULL
                OR PREV_INSTLAMOUNT <> INSTLAMOUNT THEN 1
              ELSE 0
            END
          ) OVER (
          PARTITION BY
            ARNGNO
          ORDER BY 
            INSTLDATE
          ) AS TIER_SEQ
    FROM FILTER_AA32P_PREVNEXT
),
FILTER_AA32P_TAG_INSTLDATE_END AS (
  SELECT *
         ,MAX(
            CASE
              WHEN NEXT_INSTLAMOUNT IS NULL
                OR NEXT_INSTLAMOUNT <> INSTLAMOUNT THEN INSTLDATE
            END
          ) OVER (
          PARTITION BY
            ARNGNO
           ,TIER_SEQ
          ) AS TIER_INSTLDATE_END
    FROM FILTER_AA32P_TAG_TIERSEQ
),
FILTER_AA32P_TIERCHANGE AS (
  SELECT FILTER_AA32P_TAG_INSTLDATE_END.*
         ,CASE
            WHEN TIER_INSTLDATE_END < CTRL.WS_MIGR_DATE THEN 'HISTORIC'
            WHEN INSTLDATE >= CTRL.WS_MIGR_DATE THEN 'FUTURE'
            WHEN INSTLDATE < CTRL.WS_MIGR_DATE
              AND TIER_INSTLDATE_END >= CTRL.WS_MIGR_DATE THEN 'CURRENT'
          END AS TIER_STATUS
    FROM FILTER_AA32P_TAG_INSTLDATE_END
    CROSS JOIN TB_UDMH_CTL AS CTRL
   WHERE PREV_INSTLAMOUNT IS NULL
     OR PREV_INSTLAMOUNT <> INSTLAMOUNT
)
SELECT *
  FROM FILTER_AA32P_TIERCHANGE
GO

--A07<LATEST>
IF EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_A07_LATEST]') AND TYPE IN (N'V'))
 DROP VIEW [DBO].[VW_FILTER_A07_LATEST]
GO

CREATE VIEW [DBO].[VW_FILTER_A07_LATEST]
AS
WITH 
FILTER_A07_ALL AS (
  SELECT *
        ,ROW_NUMBER() OVER (
           PARTITION BY
             X02KEY
            ,A07_SUB_ACC
           ORDER BY
             A07_POSTED_DT DESC
         ) AS RN
    FROM  TB_X02_A07
),
FILTER_A07_LATEST AS (
  SELECT *
    FROM FILTER_A07_ALL
   WHERE RN = 1
)
SELECT * 
  FROM FILTER_A07_LATEST
GO

--VMOVEMENTS<LATESTOUT>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_VMOVEMENTS_LATESTOUT]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_VMOVEMENTS_LATESTOUT]
GO
 
CREATE VIEW [DBO].[VW_FILTER_VMOVEMENTS_LATESTOUT]
AS 
WITH
FILTER_VMOVEMENTS_ALLOUTDESC AS (
  SELECT MOVEPROPSEQNO
        ,MOVEDATE
        ,MOVELASTCHNGBY
        ,MOVEREASONCODE
        ,MOVESOLSEQNO
        ,MOVETEXT
        ,MOVETYPE
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             MOVEPROPSEQNO
           ORDER BY
             MOVEDATE DESC
            ,MOVESEQNO DESC
           ) AS VMOVEMENTS_ALLOUTDESC_RN 
    FROM TB_SRC_VMOVEMENTS
   WHERE MOVETYPE <> 'I'
),
FILTER_VMOVEMENTS_LATESTOUT AS (
  SELECT *
    FROM FILTER_VMOVEMENTS_ALLOUTDESC
   WHERE VMOVEMENTS_ALLOUTDESC_RN = 1
)
SELECT *
  FROM FILTER_VMOVEMENTS_LATESTOUT
GO

--VMOVEMENTS<LATESTIN>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_VMOVEMENTS_LATESTIN]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_VMOVEMENTS_LATESTIN]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_VMOVEMENTS_LATESTIN]
AS 
WITH
FILTER_VMOVEMENTS_ALLINDESC AS (
  SELECT MOVEPROPSEQNO
        ,MOVEDATE
        ,MOVELASTCHNGBY
        ,MOVEREASONCODE
        ,MOVESOLSEQNO
        ,MOVETEXT
        ,MOVETYPE
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             MOVEPROPSEQNO
           ORDER BY
             MOVEDATE DESC
            ,MOVESEQNO DESC
           ) AS VMOVEMENTS_ALLINDESC_RN 
    FROM TB_SRC_VMOVEMENTS 
   WHERE MOVETYPE = 'I'
),
FILTER_VMOVEMENTS_LATESTIN AS (
  SELECT *
    FROM FILTER_VMOVEMENTS_ALLINDESC
   WHERE VMOVEMENTS_ALLINDESC_RN = 1
)
SELECT *
  FROM FILTER_VMOVEMENTS_LATESTIN
GO

--VMOVEMENTS<EARLIESTIN>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_VMOVEMENTS_EARLIESTIN]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_VMOVEMENTS_EARLIESTIN]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_VMOVEMENTS_EARLIESTIN]
AS 
WITH
FILTER_VMOVEMENTS_ALLINASC AS (
  SELECT MOVEPROPSEQNO
        ,MOVEDATE
        ,MOVELASTCHNGBY
        ,MOVEREASONCODE
        ,MOVESOLSEQNO
        ,MOVETEXT
        ,MOVETYPE
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             MOVEPROPSEQNO
           ORDER BY
             MOVEDATE 
            ,MOVESEQNO 
           ) AS VMOVEMENTS_ALLINASC_RN 
    FROM TB_SRC_VMOVEMENTS
   WHERE MOVETYPE = 'I'
),
FILTER_VMOVEMENTS_EARLIESTIN AS (
  SELECT *
    FROM FILTER_VMOVEMENTS_ALLINASC
   WHERE VMOVEMENTS_ALLINASC_RN = 1
)
SELECT *
  FROM FILTER_VMOVEMENTS_EARLIESTIN
GO

--VMOVEMENTS<LATEST>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_VMOVEMENTS_LATEST]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_VMOVEMENTS_LATEST]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_VMOVEMENTS_LATEST]
AS 
WITH
VMOVEMENTS_ALL AS (
  SELECT MOVEPROPSEQNO
        ,MOVEDATE
        ,MOVELASTCHNGBY
        ,MOVEREASONCODE
        ,MOVESOLSEQNO
        ,MOVETEXT
        ,MOVETYPE
        ,MOVEINTERNALCODE 
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             MOVEPROPSEQNO
           ORDER BY
             MOVEDATE DESC
            ,MOVESEQNO DESC
           ) AS VMOVEMENTS_ALL_RN
    FROM TB_SRC_VMOVEMENTS
),
FILTER_VMOVEMENTS_LATESTIN AS (
  SELECT *
    FROM VMOVEMENTS_ALL
   WHERE VMOVEMENTS_ALL_RN = 1
)
SELECT *
  FROM FILTER_VMOVEMENTS_LATESTIN
GO

--INTERESTRATES<CURRENTATMIGR>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INTERESTRATES_CURRENTATMIGR]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_INTERESTRATES_CURRENTATMIGR]
GO
 
CREATE VIEW [DBO].[VW_FILTER_INTERESTRATES_CURRENTATMIGR]
AS 
WITH
INTERESTRATES_ALL AS (
  SELECT INTRINTCCODE 
        ,INTRGROSRT
        ,INTRSUBCODE
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             INTRINTCCODE 
           ORDER BY 
             INTREFDATE DESC
           ) AS INTERESTRATES_RN
    FROM TB_SRC_INTERESTRATES 
   CROSS JOIN TB_UDMH_CTL 
   WHERE INTRSCHEDULED = 'Y'
     AND INTREFDATE < WS_MIGR_DATE
),
INTERESTRATES_CURRENTATMIGR AS (
  SELECT *
    FROM INTERESTRATES_ALL
   WHERE INTERESTRATES_RN = 1
)
SELECT *
  FROM INTERESTRATES_CURRENTATMIGR
GO

--FUNDSMOVEPAYMENTS<CURRENTATMIGR>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_FUNDSMOVEPAYMENTS_CURRENTATMIGR]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_FUNDSMOVEPAYMENTS_CURRENTATMIGR]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_FUNDSMOVEPAYMENTS_CURRENTATMIGR]
AS 
WITH
FILTER_FUNDSMOVEPAYMENTS_1 AS (
  SELECT FMPEFDATE
        ,FMPFIRSTDATE
        ,FMPFIRSTREGDATE
        ,FMPFMSEQNO
        ,FMPLASTEXTRACTDATE
        ,FMPLASTPAIDDATE
        ,FMPNEXTDUEDATE
        ,FMPREGFREQCODE
        ,FMPREGFREQINTERVAL
    FROM TB_SRC_FUNDSMOVEPAYMENTS 
   --CROSS JOIN TB_UDMH_CTL                   Commented this table because based on mapping spec no need of joining the control table in this filter and after commenting this table there is no change in the total count
   WHERE FMPLASTEXTRACTDATE = FMPNEXTDUEDATE
),
FILTER_FUNDSMOVEPAYMENTS_2 AS (
  SELECT FMPEFDATE
        ,FMPFIRSTDATE
        ,FMPFIRSTREGDATE
        ,FMPFMSEQNO
        ,FMPLASTEXTRACTDATE
        ,FMPLASTPAIDDATE
        ,FMPNEXTDUEDATE
        ,FMPREGFREQCODE
        ,FMPREGFREQINTERVAL
    FROM TB_SRC_FUNDSMOVEPAYMENTS
    CROSS JOIN TB_UDMH_CTL
   WHERE FMPNEXTDUEDATE <= DATEADD(MONTH, 1, WS_MIGR_DATE )
     AND NOT EXISTS (SELECT 1
                       FROM TB_SRC_FUNDSMOVEPAYMENTS AS RAISED
                      WHERE RAISED.FMPFMSEQNO = FMPFMSEQNO
                       AND RAISED.FMPLASTEXTRACTDATE = RAISED.FMPNEXTDUEDATE 
                    )
),
FILTER_FUNDSMOVEPAYMENTS_CURRENTATMIGR AS (
  SELECT *
    FROM FILTER_FUNDSMOVEPAYMENTS_1
  UNION ALL
  SELECT *
    FROM FILTER_FUNDSMOVEPAYMENTS_2
)
SELECT *
  FROM FILTER_FUNDSMOVEPAYMENTS_CURRENTATMIGR
GO

--FUNDSMOVEMANDATES<DIRECTDEBIT>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_FUNDSMOVEMANDATES_DIRECTDEBIT]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_FUNDSMOVEMANDATES_DIRECTDEBIT]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_FUNDSMOVEMANDATES_DIRECTDEBIT]
AS 
WITH
FILTER_FUNDSMOVEMANDATES_DIRECTDEBIT AS (
  SELECT FMCOREREF
        ,FMMANDATENO
        ,FMSEQNO
        ,FMTOACNTNO
        ,FMTOSUBACNO
        ,FMFROMBANKACSEQNO
    FROM TB_SRC_FUNDSMOVEMANDATES
   WHERE FMTYPE = 'DD'
)
SELECT *
  FROM FILTER_FUNDSMOVEMANDATES_DIRECTDEBIT
GO

--FUNDSMOVEEXTRACT<LATESTFORMANDATE>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_FUNDSMOVEEXTRACT_LATESTFORMANDATE]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_FUNDSMOVEEXTRACT_LATESTFORMANDATE]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_FUNDSMOVEEXTRACT_LATESTFORMANDATE]
AS 
WITH
FILTER_FEX_ALL AS (
  SELECT FEXFUNDEXAMOUNT
        ,FEXFUNDEXAUTHMVSEQNO
        ,FEXFUNDEXBACSDATE
        ,FEXFUNDEXREPIND
        ,FEXFUNDEXSTOPIND
        ,FEXFUNDEXDATE
        ,FEXFUNDEXPOSTEDDATE
        ,FEXFUNDEXACCOUNTNO
        ,FEXFUNDEXTRACERNO
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             FEXFUNDEXAUTHMVSEQNO
           ORDER BY
             FEXFUNDEXDATE DESC
           ) AS FEX_L4M_RN
    FROM TB_SRC_FUNDSMOVEEXTRACT
)
SELECT *
  FROM FILTER_FEX_ALL
 WHERE FEX_L4M_RN = 1
GO

--CWPHONECONTACT<FIRSTMOBILE>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_CWPHONECONTACT_FIRSTMOBILE]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_CWPHONECONTACT_FIRSTMOBILE]
GO
 
CREATE VIEW [DBO].[VW_FILTER_CWPHONECONTACT_FIRSTMOBILE]
AS
WITH
FILTER_CWMOBILE AS (
  SELECT AREACODE
        ,CUSTOMERNO
        ,EXTENSION
        ,INTCODE
        ,TELEPHONENO
        ,USAGE
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             CUSTOMERNO
           ORDER BY 
             LASTUPDATEDATE DESC
           ) AS RN
   FROM TB_SRC_CWPHONECONTACT
  WHERE USAGE = 1632007
),
FILTER_ALL_CWMOBILE AS (
  SELECT *
    FROM FILTER_CWMOBILE
   WHERE RN = 1
)
SELECT *
  FROM FILTER_ALL_CWMOBILE
GO

--CWOTHERCONTACT<LASTUPDATED>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_CWOTHERCONTACT_LASTUPDATED]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_CWOTHERCONTACT_LASTUPDATED]
GO
 
CREATE VIEW [DBO].[VW_FILTER_CWOTHERCONTACT_LASTUPDATED]
AS
WITH
FILTER_CWOTHER_ALL AS (
  SELECT FREEFORMATADD
        ,CUSTOMERNO
        ,ROW_NUMBER() OVER (
           PARTITION BY
             CUSTOMERNO
           ORDER BY
             LASTUPDATEDATE DESC
           ) AS RN
   FROM TB_SRC_CWOTHERCONTACT
),
FILTER_CWOTHER_LASTUPDATED AS (
  SELECT *
   FROM FILTER_CWOTHER_ALL
   WHERE RN = 1
)
SELECT *
  FROM FILTER_CWOTHER_LASTUPDATED
GO

--APP<LASTMADE>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_APP_LASTMADE]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_APP_LASTMADE]
GO
 
CREATE VIEW [DBO].[VW_FILTER_APP_LASTMADE]
AS
WITH FILTER_APPCUST_ALL AS (
SELECT *
    ,ROW_NUMBER() OVER (
        PARTITION BY 
           M6321
          ,C4554
        ORDER BY
            F2253
        ) AS RN_APPCUST
FROM TB_SRC_APPCUST AS APPCUST
),
FILTER_APPCUST_EARLIEST AS (
SELECT *
  FROM FILTER_APPCUST_ALL
  WHERE RN_APPCUST = 1
),

FILTER_APP_COMPLETED AS (
  SELECT A12144
        ,APP.A1237
        ,APP.A1238
        ,APP.A125123
        ,APP.A1263
        ,APP.A1265
        ,APP.A1339
        ,APP.A136192
        ,APP.A13676
        ,APP.A14179
        ,APP.A142190
        ,APP.A14223
        ,APP.A14352
        ,APP.A152140
        ,APP.A156124
        ,APP.A24220
        ,APP.B52428
        ,APP.C2563
        ,APP.M63214
        ,APP.P61416
        ,APP.P61417
        ,APP.P6143523
        ,APP.P61437
        ,APP.Q335100
        ,APP.S13427
        ,APP.S20011
        ,APP.S20015
        ,APP.S2003
        ,APP.S2007
        ,APP.S31225
        ,APP.S34526
        ,APP.S36019
        ,APP.S36021
        ,APP.S36022
        ,APP.S36023
        ,APP.S36024
        ,APP.S42518
        ,APP.V41336
        ,APP.V41339
        ,APP.V41454
        ,APP.V41537
        ,APP.V41572
        ,APP.V42116
        ,APP.V42317
        ,APP.V42338
        ,APP.V42510
        ,APP.V42512
        ,APP.V42513
        ,APP.V42514
        ,APP.V42515
        ,APP.V42525
        ,APP.V4259
        ,APP.V42653
        ,APP.V42659
        ,APP.V43120
        ,APP.V45263
        ,APP.V45361
        ,APP.V4604
        ,APP.V46169
        ,APP.V46232
        ,APP.V46527
        ,APP.V46528
        ,APP.V46529
        ,ROW_NUMBER() OVER (
           PARTITION BY
             M63214
           ORDER BY
             A1238 DESC
           ) AS RN
    FROM TB_SRC_AC01_MAIN
   INNER JOIN TB_SRC_APP AS APP
      ON ACACNTNO = APP.M63214
   WHERE APP.A12398 IN ('C','L')
     AND EXISTS (SELECT 1
                   FROM TB_SRC_APPSUB AS NOCLONE
                   WHERE NOCLONE.M63220 = APP.M63214
                     AND NOCLONE.A22010 = APP.A14223
                     AND NOCLONE.A211106 = 'N'      --As per the issue list of P01 write criteria this condition is implemented
                )
),  
FILTER_APP_COMPLETED_LAST AS (
  SELECT *
    FROM FILTER_APP_COMPLETED
   WHERE RN = 1
),
FILTER_APPCUST_LASTMADE AS (
  SELECT *
    FROM FILTER_APP_COMPLETED_LAST
   INNER JOIN FILTER_APPCUST_EARLIEST
      ON M6321 = M63214
)
SELECT *
  FROM FILTER_APPCUST_LASTMADE
GO


--VTRANSACTIONS<NOTREVERSEDORREVERSING>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_VTRANSACTIONS_NOTREVERSEDORREVERSING]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_VTRANSACTIONS_NOTREVERSEDORREVERSING]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_VTRANSACTIONS_NOTREVERSEDORREVERSING]
AS
WITH 
FILTER_VTRANSACTIONS_NOTREVERSEDORREVERSING AS (
  SELECT TRACINTGROSDB
        ,TRACNTNO
        ,TRACTCODE
        ,TRADJUSTREASON
        ,TRAMOUNTCR
        ,TRAMOUNTDB
        ,TRBALINTDB
        ,TRCAPITALBAL
        ,TRCREATEDATE
        ,TRDATE
        ,TRFORINTGROSDB
        ,TRGTRSTYPE
        ,TRINTCCODE
        ,TRINTRRT
        ,TRPAYMETHOD
        ,TRREVERSALIND
        ,TRREVERSE
        ,TRSEQNO
        ,TRSOURCE
        ,TRSUBACNO
        ,TRTRACERNO
        ,TRTRSCODE
        ,TRREFERENCE
    FROM TB_SRC_VTRANSACTIONS
   WHERE (TRREVERSALIND IS NULL OR TRREVERSALIND <> 'Y')
     AND (TRREVERSE IS NULL OR TRREVERSE <> 'Y')
)
SELECT *
  FROM FILTER_VTRANSACTIONS_NOTREVERSEDORREVERSING
GO


--INSURANCEPOLICIES<LATEST>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INSURANCEPOLICIES_LATEST]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_LATEST]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_LATEST]
AS
WITH 
INSURANCEPOLICIES_TAG_FILTER AS (
  SELECT INSPACNTNO
        ,INSPCANCELDATE
        ,INSPCANCELIND
        ,INSPCOMISION
        ,INSPCOVERFROM
        ,INSPCOVERTO
        ,INSPDEBITFREQ
        ,INSPINSTSEQNO
        ,INSPLASTCHNGDATE
        ,INSPPOLICYDATE
        ,INSPPOLICYNO
        ,INSPPOLICYSEQNO
        ,INSPPREMIUM
        ,INSPPREMIUMTAX
        ,INSPSEQNO
        ,INSPSUMINSURED
        ,INSPSUBACNO
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM DESC
           ) AS RN_LAST2FIRST
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM
           ) AS RN_FIRST2LAST
    FROM TB_SRC_INSURANCEPOLICIES
   CROSS JOIN TB_UDMH_CTL
   WHERE INSPSCHEDULED = 'Y'
     AND INSPCOVERFROM < WS_MIGR_DATE
),
INSURANCEPOLICIES_LATEST_FILTER AS (
SELECT *
  FROM INSURANCEPOLICIES_TAG_FILTER
  WHERE RN_LAST2FIRST= 1
)
SELECT *
  FROM INSURANCEPOLICIES_LATEST_FILTER
GO


--AC05C<BLOCK2>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_AC05C_BLOCK2]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_AC05C_BLOCK2]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_AC05C_BLOCK2]
AS
WITH 
AC05C_BLOCK2_FILTER AS (
  SELECT ACNTNO
        ,DAT12
        ,DAT38
        ,DAT39
        ,DAT40
        ,DAT41
        ,DAT42
        ,DAT46
        ,DAT5
        ,DATBLOCK
        ,DAT34
        ,DAT37
    FROM TB_SRC_AC05C
   WHERE DATBLOCK = 2
)
SELECT *
  FROM AC05C_BLOCK2_FILTER
GO



--AC05C<BLOCK1>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_AC05C_BLOCK1]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_AC05C_BLOCK1]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_AC05C_BLOCK1]
AS
WITH 
AC05C_BLOCK1_FILTER AS (
  SELECT ACNTNO
        ,DAT12
        ,DAT38
        ,DAT39
        ,DAT40
        ,DAT41
        ,DAT42
        ,DAT46
        ,DAT5
        ,DATBLOCK
        ,DAT34
        ,DAT37
    FROM TB_SRC_AC05C
   WHERE DATBLOCK = 1
)
SELECT *
  FROM AC05C_BLOCK1_FILTER
GO



--AC05C<BLOCK4>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_AC05C_BLOCK4]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_AC05C_BLOCK4]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_AC05C_BLOCK4]
AS
WITH 
AC05C_BLOCK4_FILTER AS (
  SELECT ACNTNO
        ,DAT12
        ,DAT38
        ,DAT39
        ,DAT40
        ,DAT41
        ,DAT42
        ,DAT46
        ,DAT5
        ,DATBLOCK
        ,DAT34
        ,DAT37
    FROM TB_SRC_AC05C
   WHERE DATBLOCK = 4
)
SELECT *
  FROM AC05C_BLOCK4_FILTER
GO


--INSURANCEPOLICIES<Futr1>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INSURANCEPOLICIES_FUTR1]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_FUTR1]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_FUTR1]
AS
WITH
INSURANCEPOLICIES_FUTRN_FILTER AS (
  SELECT INSPACNTNO
        ,INSPCANCELDATE
        ,INSPCANCELIND
        ,INSPCOMISION
        ,INSPCOVERFROM
        ,INSPCOVERTO
        ,INSPDEBITFREQ
        ,INSPINSTSEQNO
        ,INSPLASTCHNGDATE
        ,INSPPOLICYDATE
        ,INSPPOLICYNO
        ,INSPPOLICYSEQNO
        ,INSPPREMIUM
        ,INSPPREMIUMTAX
        ,INSPSEQNO
        ,INSPSUMINSURED
        ,INSPSUBACNO
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM ASC
          ) AS RN_FIRST2LAST
   FROM TB_SRC_INSURANCEPOLICIES
  CROSS JOIN TB_UDMH_CTL
  WHERE INSPSCHEDULED = 'Y'
    AND INSPCOVERFROM >= WS_MIGR_DATE
),
INSURANCEPOLICIES_FUTR1_FILTER AS (
  SELECT *
    FROM INSURANCEPOLICIES_FUTRN_FILTER
   WHERE RN_FIRST2LAST = 1
)
SELECT *
  FROM INSURANCEPOLICIES_FUTR1_FILTER
GO

--INSURANCEPOLICYRISKS<FIRSTASU>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INSURANCEPOLICYRISKS_FIRSTASU]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_INSURANCEPOLICYRISKS_FIRSTASU]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_INSURANCEPOLICYRISKS_FIRSTASU]
AS
WITH 
INSURANCEPOLICYRISKS_FILTER AS (
  SELECT INSPRCLSEQNO1
        ,INSPRCLSEQNO2
        ,INSPRINSPSEQNO
        ,INSPRMPICL1DIV
        ,INSPRMPICL1MULT
        ,INSPRPREMIUM
        ,INSPRRISKNO
        ,INSPRSUMINSURED
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPRINSPSEQNO
           ORDER BY
             INSPRSEQNO
           ) AS RN
   FROM TB_SRC_INSURANCEPOLICYRISKS
)
SELECT *
  FROM INSURANCEPOLICYRISKS_FILTER
GO

--INSURANCEPOLICIES<EARLIEST>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INSURANCEPOLICIES_EARLIEST]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_EARLIEST]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_EARLIEST]
AS
WITH 
INSURANCEPOLICIES_TAG_FILTER AS (
  SELECT INSPACNTNO
        ,INSPCANCELDATE
        ,INSPCANCELIND
        ,INSPCOMISION
        ,INSPCOVERFROM
        ,INSPCOVERTO
        ,INSPDEBITFREQ
        ,INSPINSTSEQNO
        ,INSPLASTCHNGDATE
        ,INSPPOLICYDATE
        ,INSPPOLICYNO
        ,INSPPOLICYSEQNO
        ,INSPPREMIUM
        ,INSPPREMIUMTAX
        ,INSPSEQNO
        ,INSPSUMINSURED
        ,INSPSUBACNO
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM DESC
           ) AS RN_LAST2FIRST
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM 
           ) AS RN_FIRST2LAST
    FROM TB_SRC_INSURANCEPOLICIES 
   CROSS JOIN TB_UDMH_CTL 
   WHERE INSPSCHEDULED = 'Y'
     AND INSPCOVERFROM < WS_MIGR_DATE
   
),
INSURANCEPOLICIES_EARLIEST_FILTER AS (
SELECT *
FROM INSURANCEPOLICIES_TAG_FILTER
WHERE RN_FIRST2LAST = 1
)
SELECT *
  FROM INSURANCEPOLICIES_EARLIEST_FILTER
GO


--INSURANCEPOLICIES<PREV1>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INSURANCEPOLICIES_PREV1]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_PREV1]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_PREV1]
AS
WITH 
INSURANCEPOLICIES_TAG_FILTER AS (
  SELECT INSPACNTNO
        ,INSPCANCELDATE
        ,INSPCANCELIND
        ,INSPCOMISION
        ,INSPCOVERFROM
        ,INSPCOVERTO
        ,INSPDEBITFREQ
        ,INSPINSTSEQNO
        ,INSPLASTCHNGDATE
        ,INSPPOLICYDATE
        ,INSPPOLICYNO
        ,INSPPOLICYSEQNO
        ,INSPPREMIUM
        ,INSPPREMIUMTAX
        ,INSPSEQNO
        ,INSPSUMINSURED
        ,INSPSUBACNO
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM DESC
           ) AS RN_LAST2FIRST
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM 
           ) AS RN_FIRST2LAST
    FROM TB_SRC_INSURANCEPOLICIES 
   CROSS JOIN TB_UDMH_CTL 
   WHERE INSPSCHEDULED = 'Y'
     AND INSPCOVERFROM < WS_MIGR_DATE
   
),
INSURANCEPOLICIES_PREV1_FILTER AS (
SELECT *
FROM INSURANCEPOLICIES_TAG_FILTER
WHERE RN_LAST2FIRST = 2
)
SELECT *
  FROM INSURANCEPOLICIES_PREV1_FILTER
GO


--INSURANCEPOLICIES<PREV2>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INSURANCEPOLICIES_PREV2]') AND TYPE IN (N'V'))
DROP VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_PREV2]
GO 
 
CREATE VIEW [DBO].[VW_FILTER_INSURANCEPOLICIES_PREV2]
AS
WITH 
INSURANCEPOLICIES_TAG_FILTER AS (
  SELECT INSPACNTNO
        ,INSPCANCELDATE
        ,INSPCANCELIND
        ,INSPCOMISION
        ,INSPCOVERFROM
        ,INSPCOVERTO
        ,INSPDEBITFREQ
        ,INSPINSTSEQNO
        ,INSPLASTCHNGDATE
        ,INSPPOLICYDATE
        ,INSPPOLICYNO
        ,INSPPOLICYSEQNO
        ,INSPPREMIUM
        ,INSPPREMIUMTAX
        ,INSPSEQNO
        ,INSPSUMINSURED
        ,INSPSUBACNO
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM DESC
           ) AS RN_LAST2FIRST
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM 
           ) AS RN_FIRST2LAST
    FROM TB_SRC_INSURANCEPOLICIES 
   CROSS JOIN TB_UDMH_CTL 
   WHERE INSPSCHEDULED = 'Y'
     AND INSPCOVERFROM < WS_MIGR_DATE
   
),
INSURANCEPOLICIES_PREV2_FILTER AS (
SELECT *
FROM INSURANCEPOLICIES_TAG_FILTER
WHERE RN_LAST2FIRST = 3
)
SELECT *
  FROM INSURANCEPOLICIES_PREV2_FILTER
GO

--INSURANCEPOLICIES<PREV3>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_INSURANCEPOLICIES_PREV3]') AND TYPE IN (N'V'))
DROP VIEW [DBO].VW_FILTER_INSURANCEPOLICIES_PREV3
GO 
 
CREATE VIEW [DBO].VW_FILTER_INSURANCEPOLICIES_PREV3
AS
WITH 
INSURANCEPOLICIES_TAG_FILTER AS (
  SELECT INSPACNTNO
        ,INSPCANCELDATE
        ,INSPCANCELIND
        ,INSPCOMISION
        ,INSPCOVERFROM
        ,INSPCOVERTO
        ,INSPDEBITFREQ
        ,INSPINSTSEQNO
        ,INSPLASTCHNGDATE
        ,INSPPOLICYDATE
        ,INSPPOLICYNO
        ,INSPPOLICYSEQNO
        ,INSPPREMIUM
        ,INSPPREMIUMTAX
        ,INSPSEQNO
        ,INSPSUMINSURED
        ,INSPSUBACNO
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM DESC
           ) AS RN_LAST2FIRST
        ,ROW_NUMBER() OVER (
           PARTITION BY
             INSPACNTNO
            ,INSPSUBACNO
           ORDER BY
             INSPCOVERFROM 
           ) AS RN_FIRST2LAST
    FROM TB_SRC_INSURANCEPOLICIES 
   CROSS JOIN TB_UDMH_CTL 
   WHERE INSPSCHEDULED = 'Y'
     AND INSPCOVERFROM < WS_MIGR_DATE
   
),
INSURANCEPOLICIES_PREV3_FILTER AS (
SELECT *
FROM INSURANCEPOLICIES_TAG_FILTER
WHERE RN_LAST2FIRST = 4
)
SELECT *
  FROM INSURANCEPOLICIES_PREV3_FILTER
GO


--PAYMENTELEMENTS<CurrentAtMigr>
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_PAYMENTELEMENTS_CURRENTATMIGR]') AND TYPE IN (N'V'))
DROP VIEW [DBO].VW_FILTER_PAYMENTELEMENTS_CURRENTATMIGR
GO 
 
CREATE VIEW [DBO].VW_FILTER_PAYMENTELEMENTS_CURRENTATMIGR
AS
WITH
CTRL AS (
  SELECT CAST('20150925' AS DATE) AS MIGR_DATE
)
SELECT *
  FROM TB_SRC_PAYMENTELEMENTS
 CROSS JOIN CTRL
 WHERE PAYENEXTDUEDATE >= CAST('20150901' AS DATE)
   AND PAYESCHEDULED = 'Y'
   AND (PAYELASTDATE IS NULL
       OR PAYELASTDATE >= MIGR_DATE)
   AND PAYEFIRSTDATE <= DATEADD(MONTH,1,MIGR_DATE)

GO

--HLDLNKS
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_HLDLNKS]') AND TYPE IN (N'V'))
DROP VIEW [DBO].VW_FILTER_HLDLNKS
GO 
 
CREATE VIEW [DBO].VW_FILTER_HLDLNKS
AS
WITH
FILTER_HLDLNKS AS (
  SELECT HLDLACNTNO
        ,HLDLCODE
        ,HLDLFROMDATE
        ,HLDLLASTCHNGBY
        ,HLDLTODATE
        ,ROW_NUMBER() OVER (
           PARTITION BY 
             HLDLACNTNO
            ,HLDLCODE
              ORDER BY HLDLFROMDATE DESC
           ) AS DUPE_CNT
    FROM TB_SRC_HLDLNKS
)
SELECT *
  FROM FILTER_HLDLNKS
  WHERE DUPE_CNT = 1

GO


--APPSUB
IF  EXISTS (SELECT * FROM SYS.OBJECTS WHERE OBJECT_ID = OBJECT_ID(N'[DBO].[VW_FILTER_APPSUB]') AND TYPE IN (N'V'))
DROP VIEW [DBO].VW_FILTER_APPSUB
GO 
 
CREATE VIEW [DBO].VW_FILTER_APPSUB
AS
WITH
FILTER_APPSUB AS (
  SELECT A16246
        ,A16247
        ,A211106
        ,A21284
        ,A22010
        ,A2501
        ,A253107
        ,M63220
        ,ROW_NUMBER() OVER (
           PARTITION BY
             M63220
            ,A253107
           ORDER BY
             A22010 DESC
         ) AS RN
  FROM TB_SRC_APPSUB
)
SELECT *
  FROM FILTER_APPSUB
 WHERE RN = 1

GO